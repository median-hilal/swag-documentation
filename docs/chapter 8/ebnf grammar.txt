sparqlTemplate = (SPARQLTEXT | patternFunction), {SPARQLTEXT |
patternFunction};
patternFunction = singleton | spaceList | patternList | filterList |
joinFilterList;
singleton = '!E', (baseElement | asClause), '!E';
spaceList = '!SL', (baseElement | asClause), '!SL';
patternList = '!DL', baseElement, '!DL';
filterList = '!FL', baseElement, '!FL';
joinFilterList = '!JFL', baseElement, '=', baseElement, '!JFL';
asClause = '![', baseElement, ['AS', ['!VM'], ((prefix, baseElement) | SPARQLTEXT)], '!]';
baseElement = elementRole | prefixedElementRole;
prefixedElementRole = ['!VM'], prefix, elementRole;
prefix = SPARQLTEXT, '!+';
elementRole = '<', ID, attribute, '>';
attribute = '.', ID;
ID = ('_' | letter | digit) , {letter | digit | '_' | '-'};
SPARQLTEXT = '"', {character}, '"';
letter = "A" | "B" | "C" | "D" | "E" | "F" | "G"
       | "H" | "I" | "J" | "K" | "L" | "M" | "N"
       | "O" | "P" | "Q" | "R" | "S" | "T" | "U"
       | "V" | "W" | "X" | "Y" | "Z" | "a" | "b"
       | "c" | "d" | "e" | "f" | "g" | "h" | "i"
       | "j" | "k" | "l" | "m" | "n" | "o" | "p"
       | "q" | "r" | "s" | "t" | "u" | "v" | "w"
       | "x" | "y" | "z" ;

digit = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;
character = letter | digit | symbol | "_" | " " ;
symbol = "[" | "]" | "{" | "}" | "(" | ")" | "<" | ">"
       | "'" | "=" | "|" | "." | "," | ";" | "-" 
       | "+" | "*" | "?" | "\n" | "\t" | "\r" | "\f" | "\b" ;